plugins {
    id "java"
    id "idea"
    id "org.springframework.boot" version "3.5.7"
    id "com.gorylenko.gradle-git-properties" version "2.4.2"
    id "com.adarshr.test-logger" version "4.0.0"
    id "io.spring.dependency-management" version "1.1.7"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

ext {
    springBootVersion = "3.5.7"
    mapstructVersion = "1.6.3"
    jetbrainsAnnotationsVersion = "22.0.0"
    postgresContainerVersion = "1.21.3"
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
    }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-starter-actuator"

    implementation "org.postgresql:postgresql"
    implementation "org.flywaydb:flyway-database-postgresql"

    implementation "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"
    implementation "org.mapstruct:mapstruct:$mapstructVersion"
    implementation "org.apache.commons:commons-lang3"

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.boot:spring-boot-testcontainers"
    testImplementation "org.testcontainers:postgresql:$postgresContainerVersion"
}

processResources {
    filesMatching(["**/properties-from-gradle.properties"]) {
        expand(project.properties)
    }
}

gitProperties {
    dateFormat = "dd.MM.yyyy HH:mm:ss"
    dateFormatTimeZone = "Europe/Moscow"
    extProperty = "gitProps"
}

generateGitProperties.outputs.upToDateWhen { false }

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

testlogger {
    theme "mocha"
}

bootJar {
    archiveFileName = "${project.name}.jar"
    manifest {
        attributes(
            "Application-Name": "${rootProject.name}",
            "Application-Version": "${project.version}",
            "Git-Branch": "${-> project.ext.gitProps["git.branch"]}",
            "Build-Revision": "${-> project.ext.gitProps["git.commit.id.abbrev"]}",
            "Commit-Time": "${-> project.ext.gitProps["git.commit.time"]}"
        )
    }
}
