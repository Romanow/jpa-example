plugins {
    id "idea"
    id "org.jetbrains.kotlin.jvm" version "1.9.25"
    id "org.jetbrains.kotlin.plugin.jpa" version "1.9.25"
    id "org.jetbrains.kotlin.plugin.spring" version "1.9.25"
    id "org.jetbrains.kotlin.kapt" version "1.9.25"
    id "org.jlleitschuh.gradle.ktlint" version "12.1.2"
    id "org.springframework.boot" version "3.5.8"
    id "com.gorylenko.gradle-git-properties" version "2.4.2"
    id "com.adarshr.test-logger" version "4.0.0"
    id "io.spring.dependency-management" version "1.1.7"
}

ext {
    springBootVersion = "3.5.7"
    mapstructVersion = "1.6.3"
    jetbrainsAnnotationsVersion = "22.0.0"
    postgresContainerVersion = "1.21.3"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

[compileKotlin, compileTestKotlin]*.kotlinOptions*.jvmTarget = "21"

ktlint {
    version.set("1.1.1")
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
    }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-starter-actuator"

    implementation "org.postgresql:postgresql"
    implementation "org.flywaydb:flyway-database-postgresql"

    implementation "org.apache.commons:commons-lang3"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
    implementation "org.mapstruct:mapstruct:$mapstructVersion"
    kapt "org.mapstruct:mapstruct-processor:$mapstructVersion"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.boot:spring-boot-testcontainers"
    testImplementation "org.testcontainers:postgresql:$postgresContainerVersion"
}

processResources {
    filesMatching(["**/properties-from-gradle.properties"]) {
        expand(project.properties)
    }
}

gitProperties {
    dateFormat = "dd.MM.yyyy HH:mm:ss"
    dateFormatTimeZone = "Europe/Moscow"
    extProperty = "gitProps"
}

generateGitProperties.outputs.upToDateWhen { false }

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

testlogger {
    theme "mocha"
}

bootJar {
    archiveFileName = "${project.name}.jar"
    manifest {
        attributes(
            "Application-Name": "${rootProject.name}",
            "Application-Version": "${project.version}",
            "Git-Branch": "${-> project.ext.gitProps["git.branch"]}",
            "Build-Revision": "${-> project.ext.gitProps["git.commit.id.abbrev"]}",
            "Commit-Time": "${-> project.ext.gitProps["git.commit.time"]}"
        )
    }
}
